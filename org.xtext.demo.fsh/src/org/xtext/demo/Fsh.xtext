grammar org.xtext.demo.Fsh with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate fsh "http://www.xtext.org/demo/Fsh"

Model:
	statements+=Statement*;
	
Statement:
	Expression | Definition;

Definition:
	'(' 'defun' name=ID '(' (args+=DeclaredParameter)* ')' expr=Expression ')';

DeclaredParameter:
	name=ID;

CompareExpression: 
	'(' ({GT}'>' | {LT}'<' | {GE}'>=' | {LE}'<=' | {EQ}'=') left=Expression right=Expression ')'
;

IfStatement:
	'(' 'if' cond=Expression truePart=Expression falsePart=Expression ')';


Expression:
	{NumberLiteral} value=NUMBER |	
	'(' ({Plus}'+' | {Minus}'-' | {Multi}'*' | {Div}'/') left=Expression right=Expression ')' |
	{Param}param=[DeclaredParameter] | 
	{FunctionCall} '(' func=[Definition]  (args+=Expression)* ')' |
	CompareExpression |
	IfStatement;

terminal NUMBER returns ecore::EBigDecimal:
	('0'..'9')* ('.' ('0'..'9')+)?;
	
terminal INT returns ecore::EInt:
	'this one has been deactivated';
